###############################################################################
#
#	This is a partial conversion of the rule file from the IceTagger 
#	part-of-speech tagger for Icelandic by Hrafn Loftsson, along with 
#       some hacked up rules from Trond Trosterud's Faroese CG and some of 
#       our very own home-brew crack.
#
###############################################################################

SOFT-DELIMITERS = "<,>" ;
DELIMITERS = "<.>"ri "<:>"ri "<!>"ri "<?>"ri ;

LIST BOS = (>>>) (<s>) (sent) ; 
LIST EOS = (<<<) (</s>) (sent) ; 

LIST CLB = (sent) (cnjsub); 

LIST N = (n) ;
LIST Num = (num) ;
LIST ProperNom = (np) ;
LIST Adj = (adj) ;
LIST Adv = (adv) ;
LIST PersPron = (prn p1) (prn p2) (prn p3) ;
LIST IndefPron = (prn ind);
LIST DemPron = (prn dem) ;
LIST ReflexivePronoun = (prn ref) ;
LIST InterrogativePronoun = (prn itg) ;
LIST PossessivePronoun = (prn pos) ;
LIST Pron = (prn);
LIST Prep = (pr) ;
LIST Participle = (pp) ;
LIST Inf = (inf) ;
LIST VerbInf = (vblex inf) ;
LIST VerbSpecAux = (vaux) ;
LIST VerbAux = (vaux) (vbhaver) (vbser);
LIST VerbSupine = (vblex supn) ;
LIST VerbBe = (vbser) ;
LIST Verb = (vblex) (vbser) (vbhaver);
LIST TempsFin = (pri) (past) (prs) (pss);
LIST TempsIrreal = (prs) (pss);
LIST RelativeConjunction = (rel) ;
LIST Conjunction = (cnjcoo) (cnjadv) (cnjsub) ;
LIST CC = (cnjcoo) ;
LIST CS = (cnjsub) ;
LIST Exclamation = (ij) ;
LIST Article = (def) ;
LIST Det = (det) ;
LIST Interj = (itg) ;

LIST Def = (def) ;
LIST Indef = (ind) ;

LIST Fem = (f);
LIST Msc = (m);
LIST Neu = (nt);

LIST Sg = (sg);
LIST Pl = (pl);

LIST Nom = (nom);
LIST Gen = (gen);
LIST Acc = (acc);
LIST Dat = (dat);

LIST Inf = (inf);

LIST GenPrep = "án"ri "auk"ri "austan"ri "innan"ri "í staö"ri "meðal"ri "megin"ri "milli"ri "millum"ri 
               "neðan"ri "norðan"ri "ofan"ri "sakir"ri "sunnan"ri "sökum"ri "til"ri "utan"ri "vegna"ri "vestan"ri ;
LIST AccPrep = "fyrir ofan"ri "gegnum"ri "kringum"ri "um"ri "umfram"ri "umhverfis"ri ;
LIST DatPrep = "að"ri "af"ri "andspænis"ri "ásamt"ri "frá"ri "gagn"ri "gagnvart"ri "gegnt"ri "handa"ri 
               "hjá"ri "meðfram"ri "mót"ri "móti"ri "nálægt"ri "undan"ri "úr"ri ;
LIST AccDatPrep = "á"ri "eftir"ri "fyrir"ri "í"ri "með"ri "undir"ri "við"ri "yfir"ri ;
LIST MODV = "ætla" "kunna" "láta" "skula" "vilja" "munu" "mega" "vera" ; #todo: translate fao to icelandic
LIST SADV = "aldrei" "ekki" ; # complete

LIST NAGD = nom acc gen dat;
LIST GENDER = m f nt;
LIST NUMBER = sg pl;
LIST PERSON = p1 p2 p3;

LIST COMMA = (",") ;

SET VerbFin = Verb + TempsFin;

SET NomMscFem = (n m) OR (n f) ;
SET NomMscNeu = (n m) OR (n nt) ;
SET NomFemNeu = (n f) OR (n nt) ;

SET NOTDAT = Nom | Gen | Acc ;
SET NOTACC = Nom | Gen | Dat ;
SET NOTACCDAT = Nom | Gen ;
SET OBL = Acc | Dat | Gen ;

SET POST-DET = Adj | Det | N | Num | Adv ;
SET PRE-N =  Adj | Det | (N Gen) | Num | (Pron Gen) | (Pron Dem) | CC ; # Det???

SET WORD = N | Verb | Adj | Prep | Pron | Det | Adv | CC | CS | Interj | Num | ("\?") ;
SET MARK =  COMMA | ("\") | ("\;") ; #"
SET WORDMARK = WORD | MARK ;

SET NPNHA = WORDMARK - PRE-N - Adv ;


SECTION

# Specific lexemes

REMOVE N IF (-1C N OR ProperNom) (0 ("<á>"ri));

#################################################################################
# Proper nouns
#################################################################################

# hús Láru

SELECT ProperNom IF (-1C N) (1 Gen) ; 					# !tag.isCase(GENITIVE) ;
SELECT ProperNom IF (-1C GenPrep) (1 Gen) ;	
SELECT ProperNom IF (-1C AccPrep) (1 Acc) ;
SELECT ProperNom IF (-1C DatPrep) (1 Dat) ;

# Hildar Guðmundsdóttur
SELECT ProperNom IF (-1C ProperNom) (-1C Gen) (0 ("<.*dóttur">)) (0 Gen) ; 	# !tag.isCase(GENITIVE) ;
SELECT ProperNom IF (-1C ProperNom) (-1C Fem) ; 				# !tag.isGender(FEMININE) ;
SELECT ProperNom IF (-1C ProperNom) (-1C Msc) ; 				# !tag.isGender(MASCULINE) ;

#################################################################################
# Noms
#################################################################################

# hann sýnir but stakk hún hendi is ok, það sýnir aldrei

# CEx. Þessi íslenska kona
#SELECT Nom IF ((-1C PersPron) OR (-1C PersPron LINK -1C DemPron) OR (-1C ("<við>"ri)));
# CEx. um nýja samkomulagið
#SELECT N IF (-1C GenPrep) (1 Gen) ;	
#SELECT N IF (-1C AccPrep) (1 Acc) ;
#SELECT N IF (-1C DatPrep) (1 Dat) ;

# að (cn/aa) beina (sgn/n*) mér
# Íslendingar munu boða að
SELECT N IF (-1 Inf) (0 VerbInf) (NOT -1 MODV) ;

# einhvern sem sæti
SELECT N IF (-1 ("<sem>"ri)) (0 Verb) ;

# choose the verb
# CEx. Ísland á dagskrá eftir viku
#SELECT N IF (-1C ProperNom) (0 Verb) ;

# Disallow two nouns in a row unless the latter is in the genitive case
REMOVE N IF (-1C N) (NOT -1 Gen) ((NOT -2 ProperNom) OR (-2 BOS));

# select the possessive pronoun, "pabbi sinn"
SELECT PossessivePronoun IF (-1C N);

SELECT ("á"ri) IF (-1 Adv);

# skal (ekki) hita
SELECT VerbInf IF ((-1 VerbSpecAux) OR (-1C Adv LINK NOT -2 BOS LINK -2 VerbSpecAux));

# hafði talið, velja sagnbót
SELECT VerbSupine IF (-1 VerbAux);

# select the adjective, but ok is "fyrir framan X", and ok is "bara verð markaðarins"
SELECT Adv IF (0 Adj) (-1 BOS OR (NOT -1 Prep)) (1 EOS OR (NOT 1 N LINK 1 Gen));

# sú líkn
# CEx. að í því hefðu fyrirvararnir
#SELECT DemPron IF (-1 N) (NOT 0 N);

#SELECT VerbBe IF (NOT 0 N);

# select the adjective, "hann er lasinn",
SELECT Adj IF (-1 VerbBe) (NOT -2 BOS) (-2 PersPron);


#################################################################################
# Adjectives
#################################################################################

# Hann borðar mjög heita súpu en hún hleypur.
REMOVE N IF (-1C ("<mjög>"));
REMOVE VerbInf IF (-1C ("<mjög>"));
REMOVE VerbFin IF (-1C ("<mjög>"));

# norska ríkisstjórnin
REMOVE N IF (0 N) (0 Nom) (0 Adj) (0 (vei)) (1C N) (1C Def);

# íslenskan almenning 
REMOVE N IF (0 N) (0 Nom) (0 Adj) (0 (sta)) (1C N) (1C Indef);

# kröfurnar voru skýrar;
REMOVE Adv IF (0 Adv) (0 Adj) (-1C VerbBe) (1C CLB);

# Fallega rauða rósin
REMOVE Adv IF (NOT -1C* VerbFin BARRIER CLB) (0 Adv) (0 Adj) (1 Adj);

#################################################################################
# Adverbs
#################################################################################

SELECT Adv IF (-1C VerbFin) (1C Verb) (1 Participle);

#################################################################################
# Personal pronouns 
#################################################################################


#################################################################################
# Indefinite pronouns 
#################################################################################


#################################################################################
# Demonstrative pronouns 
#################################################################################

# Ég sá ekki 
REMOVE DemPron IF (-1C PersPron);

# En mér leið ekki vel eftir þetta.
REMOVE Det IF (0 Det) (0 Pron) (NOT 1 POST-DET);

#################################################################################
# Reflexive pronouns 
#################################################################################


#################################################################################
# Interrogative pronouns 
#################################################################################


#################################################################################
# Possessive pronouns 
#################################################################################


#################################################################################
# Prepositions
#################################################################################

#að styðja 
SELECT Prep IF (0 ("að"ri)) (1 VerbInf);

#sem leggja fram kæru á hendur ríkinu.
SELECT Prep IF (0 ("[Áá]"ri)) (1C N OR ProperNom) (NOT 1C Gen);

#################################################################################
# NPs  
#################################################################################

REMOVE:ufc_f  Adj + Fem IF (0  $$NAGD) (*1 NomMscNeu + $$NAGD BARRIER NPNHA LINK NOT 0 Fem);
REMOVE:ufc_m  Adj + Msc IF (0  $$NAGD) (*1 NomFemNeu + $$NAGD BARRIER NPNHA LINK NOT 0 Msc);
REMOVE:ufc_nt Adj + Neu IF (0  $$NAGD) (*1 NomMscFem + $$NAGD BARRIER NPNHA LINK NOT 0 Neu);

REMOVE:d_ufc_f  Det + Fem IF (0  $$NAGD) (*1 NomMscNeu + $$NAGD BARRIER NPNHA LINK NOT 0 Fem);
REMOVE:d_ufc_m  Det + Msc IF (0  $$NAGD) (*1 NomFemNeu + $$NAGD BARRIER NPNHA LINK NOT 0 Msc);
REMOVE:d_ufc_nt Det + Neu IF (0  $$NAGD) (*1 NomMscFem + $$NAGD BARRIER NPNHA LINK NOT 0 Neu);

SELECT:r28 $$GENDER IF (-1C Adj + $$GENDER)(0 N);
SELECT:r28b $$GENDER IF (-1C Det + $$GENDER)(0 N);
SELECT:r28c $$GENDER IF (-1C IndefPron + $$GENDER)(0 N);

SELECT:r29 $$NAGD IF (-1C Adj + $$NAGD)(0 N);
SELECT:r29b $$NAGD IF (-1C Det + $$NAGD)(0 N);
SELECT:r29c $$NAGD IF (-1C IndefPron + $$NAGD)(0 N);

SELECT:r30 $$NUMBER IF (-1C Adj + $$NUMBER)(0 N);
SELECT:r30b $$NUMBER IF (-1C Det + $$NUMBER)(0 N);
SELECT:r30c $$NUMBER IF (-1C IndefPron + $$NUMBER)(0 N);

# enga bók
SELECT:r31 Det IF (1C N) (0 Det) (0 IndefPron);

#################################################################################
# Verbs
#################################################################################

REMOVE (imp) (NOT -1 CLB); 


# Ég sé hesta

REMOVE TempsIrreal + VerbAux IF (NOT -1* CS) (NOT -1 BOS);

SELECT:r51 Inf IF (*-1 MODV BARRIER Verb);

# María mun koma
SELECT:r52 MODV IF (1C Inf);

# Íslendingar munu boða að þeir ætli að draga úr losun
SELECT MODV + TempsFin IF (1C Inf);

# Ég las bókina
# #á morgun í Kaupmannahöfn, að því er Ríkisútvarpið greindi frá. 
SELECT:r53 $$PERSON IF (-1 PersPron + $$PERSON)(0 VerbFin) (NOT 1 ("er"ri));

# The modals munu and skulu can never be preceded by another auxiliary. (p.10)
REMOVE:r54 VerbAux IF (1C ("munu") OR ("skulu")); 

REMOVE:r55 VerbFin IF (-1C* VerbFin BARRIER CLB) (0 VerbFin) ;

# *NATO sendi 7.000 hermenn
REMOVE VerbFin + (p1) IF (NOT -1* (prn p1) BARRIER CLB) (NOT 1* (prn p1) BARRIER CLB) ;
REMOVE VerbFin + (p2) IF (NOT -1* (prn p2) BARRIER CLB) (NOT 1* (prn p2) BARRIER CLB) ;

#Bandaríkin hafa beðið þær 43 þjóðir sem eru með hermenn í Afganistan að senda 10.000 hermenn
SELECT ("hafa"ri) + VerbFin IF (1C Participle);


#################################################################################
# Syntax  
#################################################################################

## Todo: 
##       Disambiguate co-ordinators, find objects, predicates, adverbials, just 
##       enough to let us write transfer rules more easily.

SECTION

LIST @←SUBJ = @←SUBJ;
LIST @SUBJ→ = @SUBJ→;
LIST @-FMAINV = @-FMAINV;
LIST @+FMAINV = @FMAINV; 
LIST @-FAUXV = @-FAUXV;
LIST @+FAUXV = @FAUXV; 
LIST @←N = @←N;
LIST @N→ = @N→;
LIST @CNP = @CNP; # SN co-ordinator
LIST @CVP = @CVP; # SV co-ordinator

MAP (@←SUBJ) TARGET PersPron + Nom (-1C* VerbFin BARRIER CLB) (0C PersPron); 
MAP (@SUBJ→) TARGET PersPron + Nom (1C* VerbFin BARRIER CLB) (0C PersPron);

MAP (@+FAUXV) TARGET VerbAux + TempsFin (NOT 1C* VerbAux + TempsFin BARRIER CLB) 
                                        (NOT -1C* VerbAux + TempsFin BARRIER CLB) 
                                        (0C VerbAux + TempsFin) (1C* Participle OR Inf BARRIER CLB + PRE-N);

MAP (@-FMAINV) TARGET Participle (-1C* @+FAUXV BARRIER CLB);

MAP (@N→) TARGET (num) (0C (num)) (1C N);
